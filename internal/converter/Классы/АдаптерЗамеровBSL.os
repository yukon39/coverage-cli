#Область ОписаниеПеременных

Перем ИмяФайлаЗамеров;

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура РазобратьЗамеры(Конвертер) Экспорт
	
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.ОткрытьФайл(ИмяФайлаЗамеров, КодировкаТекста.UTF8);
	
	ЧтениеJSON.Прочитать(); // Начало объекта
	ЧтениеJSON.Прочитать(); // Имя свойства "Durability"
	ЧтениеJSON.Прочитать(); // Значение свойства (Число)
	ЧтениеJSON.Прочитать(); // Имя свойства "Data"
	ЧтениеJSON.Прочитать(); // Начало Массива

	Пока ЧтениеJSON.Прочитать() И ЧтениеJSON.ТипТекущегоЗначения = ТипЗначенияJSON.НачалоОбъекта Цикл
		
		ДанныеПокрытияМодуля = ПрочитатьJSON(ЧтениеJSON);
		ModuleId = ДанныеПокрытияМодуля.ModuleId;

		СтрокаЗамера = Конвертер.НоваяСтрокаЗамера();
		ЗаполнитьЗначенияСвойств(СтрокаЗамера, ModuleId);

		Для Каждого LineInfo Из ДанныеПокрытияМодуля.LineInfo Цикл

			СтрокаЗамера.LineNo = LineInfo.LineNo;
			Конвертер.РазобратьДанныеЗамера(СтрокаЗамера);

		КонецЦикла;

	КонецЦикла;
	
	ЧтениеJSON.Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриСозданииОбъекта(ПараметрИмяФайлаЗамеров)
	ИмяФайлаЗамеров = ПараметрИмяФайлаЗамеров;
КонецПроцедуры

#КонецОбласти