
#Использовать "../../../pkg/edtcoverage"
#Использовать "../internal/localization"

Перем Лог;
Перем ЛокализованныеСтроки;

#Область КомандаПриложения

Процедура ОписаниеКоманды(Команда) Экспорт

	УтилитыКомандПриложения.ДобавитьОписаниеПараметровСоединения(Команда);

	Команда.Опция("o output", , ЛокализованныеСтроки.ОписаниеОпцииФайлРезультата)
		.ТСтрока()
		.Обязательный();
	
	Команда.Опция("p password", , ЛокализованныеСтроки.ОписаниеОпцииПарольОтладчика)
		.ТСтрока()
		.ВОкружении("DBGS_PASSWORD");

	Команда.Опция("P Path", "", ЛокализованныеСтроки.ОписаниеОпцииПутьКПроекту)
		.ТСтрока();

	Команда.Опция("s src", "", ЛокализованныеСтроки.ОписаниеОпцииПутьКИсходникам)
		.ТСтрока();
	
	Команда.Опция("t timeout", ТаймаутПоУмолчанию(), ЛокализованныеСтроки.ОписаниеОпцииТаймаут)
		.ТЧисло();

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	Лог = ПараметрыПриложения.Лог();
	
	УтилитыКомандПриложения.ВключитьРежимОтладки(Команда);

	ИмяФайла       = Команда.ЗначениеОпции("output");
	
	ПарольОтладки  = Команда.ЗначениеОпции("password");

	ПутьКПроекту  = Команда.ЗначениеОпции("Path");
	ПутьКИсходникам  = Команда.ЗначениеОпции("src");

	ТаймаутОжидания = Команда.ЗначениеОпции("timeout");

	ПараметрыСоединения = УтилитыКомандПриложения.НовыеПараметрыСоединения(Команда);
	
	МенеджерПокрытия = Новый МенеджерПокрытияEDT();
	МенеджерПокрытия.УстановитьПараметрыСоединения(ПараметрыСоединения);

	Попытка
		
		ПроцессПокрытия = МенеджерПокрытия.ЗапуститьСборПокрытия(ИмяФайла, ПутьКПроекту, ПутьКИсходникам);
		ОжиданиеОшибки(ПроцессПокрытия, ТаймаутОжидания);
	
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		Лог.КритичнаяОшибка(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Лог.Отладка(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗавершитьРаботу(-1);
	
	КонецПопытки;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриСозданииОбъекта() Экспорт
	Лог = ПараметрыПриложения.Лог();
	ЛокализованныеСтроки = ЛокализованныеРесурсыКомандаСтарт.ЛокализованныеСтроки();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОжиданиеОшибки(ПроцессПокрытия, ТаймаутОжидания)

	Для Счетчик = 1 По ТаймаутОжидания Цикл

		Приостановить(1000);
		
		Если ПроцессПокрытия.Завершен Тогда
			ВызватьИсключение  СтрШаблон(ЛокализованныеСтроки.ИсключениеОшибкаЗапуска, ПроцессПокрытия.КодВозврата);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Функция ТаймаутПоУмолчанию()
	Возврат 15;
КонецФункции

#КонецОбласти
