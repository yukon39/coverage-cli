
#Использовать "../../coverage"
#Использовать "../internal/localization"

Перем Лог;
Перем ЛокализованныеСтроки;

#Область КомандаПриложения

Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Опция("i input", "", ЛокализованныеСтроки.КаталогСОтчетами)
		.ТСтрока()
		.Обязательный();	

	Команда.Опция("o output", "", ЛокализованныеСтроки.ОтчетGenericCoverage)
		.ТСтрока()
		.Обязательный();	

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	КаталогСОтчетами = Команда.ЗначениеОпции("input");
	ФайлXML          = Команда.ЗначениеОпции("output"); 

	ДанныеОтчета = Новый Соответствие;

	ФайлыОтчетов = НайтиФайлы(КаталогСОтчетами, "*.*");
	Для Каждого ФайлОтчета Из ФайлыОтчетов Цикл

		Если ФайлОтчета.Расширение = ".json" Тогда
			ДанныеПокрытияФайл = ФорматДанныеПокрытия.ПрочитатьДанныеПокрытияJSON(ФайлОтчета.ПолноеИмя);
		ИначеЕсли ФайлОтчета.Расширение = ".xml" Тогда
			ДанныеПокрытияФайл = ФорматДанныеПокрытия.ПрочитатьДанныеПокрытияXML(ФайлОтчета.ПолноеИмя);
		Иначе
			Продолжить;
		КонецЕсли;

		Для Каждого ДанныеПокрытияМодуляФайл Из ДанныеПокрытияФайл Цикл

			КлючДанных = ДанныеПокрытияМодуляФайл.SourcePath;
			ДанныеПокрытияМодуля = ДанныеОтчета.Получить(КлючДанных);
			Если ДанныеПокрытияМодуля = Неопределено Тогда
				ДанныеОтчета.Вставить(КлючДанных, ДанныеПокрытияМодуляФайл);
			Иначе	
				ФорматДанныеПокрытия.ДополнитьДанныеПокрытия(ДанныеПокрытияМодуля, ДанныеПокрытияМодуляФайл);
			КонецЕсли;	
		
		КонецЦикла;
	
	КонецЦикла;

	ФорматДанныеПокрытия.ЗаписатьДанныеПокрытияXML(ФайлXML, ДанныеОтчета);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриСозданииОбъекта() Экспорт
	Лог = ПараметрыПриложения.Лог();
	ЛокализованныеСтроки = ЛокализованныеРесурсыКомандаОтчетПокрытия.ЛокализованныеСтроки();
КонецПроцедуры

#КонецОбласти
