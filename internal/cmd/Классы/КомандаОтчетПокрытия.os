
#Использовать "../../coverage"
#Использовать "../internal/localization"
#Использовать "../../../internal/utils"

Перем Лог;
Перем ЛокализованныеСтроки;

#Область КомандаПриложения

Процедура ОписаниеКоманды(Команда) Экспорт

	Команда.Опция("i input", "", ЛокализованныеСтроки.КаталогСОтчетами)
		.ТСтрока()
		.Обязательный();	

	Команда.Опция("o output", "", ЛокализованныеСтроки.ОтчетGenericCoverage)
		.ТСтрока()
		.Обязательный();	

КонецПроцедуры

Процедура ВыполнитьКоманду(Знач Команда) Экспорт

	КаталогСОтчетами = Команда.ЗначениеОпции("input");
	ФайлXML          = Команда.ЗначениеОпции("output"); 

	ДанныеОтчета = Новый ПокрытиеGenericCoverage;

	ФайлыОтчетов = НайтиФайлы(КаталогСОтчетами, "*.*");
	Для Каждого ФайлОтчета Из ФайлыОтчетов Цикл

		Если ФайлОтчета.Расширение = ".json" Тогда
			ДанныеПокрытияФайл = Новый ВнутреннийФорматПокрытия;
		ИначеЕсли ФайлОтчета.Расширение = ".xml" Тогда
			ДанныеПокрытияФайл = Новый АдаптерGenericCoverage;
		Иначе
			Продолжить;
		КонецЕсли;

		ДанныеПокрытияФайл.Прочитать(ФайлОтчета.ПолноеИмя);
		ДанныеПокрытияФайл.ДополнитьGenericCoverage(ДанныеОтчета);
	
	КонецЦикла;

	ДанныеОтчета.Записать(ФайлXML);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриСозданииОбъекта() Экспорт
	Лог = ПараметрыПриложения.Лог();
	ЛокализованныеСтроки = ЛокализованныеРесурсыКомандаОтчетПокрытия.ЛокализованныеСтроки();
КонецПроцедуры

#КонецОбласти
