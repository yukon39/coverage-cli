#Использовать "../internal/localization"

#Использовать asserts

#Область ОписаниеПеременных

Перем ПредметОтладки;
Перем СерверОтладки;
Перем МаксимальныйРазмерКучи;
Перем НачальныйРазмерКучи;
Перем ЛокализованныеСтроки;

#КонецОбласти

#Область ПрограммныйИнтерфейс

Процедура УстановитьПредметОтладки(ПараметрПредметОтладки) Экспорт
	
	// BSLLS:NestedFunctionInParameters-off
	Утверждения.ПроверитьРавенство(ТипЗнч(ПараметрПредметОтладки), Тип("Строка"), 
		ЛокализованныеСтроки.УтверждениеПредметОтладкиТип);
	// BSLLS:NestedFunctionInParameters-on
	
	Если ЗначениеЗаполнено(ПараметрПредметОтладки) Тогда
		ПредметОтладки = ПараметрПредметОтладки;
	Иначе
		ПредметОтладки = ПредметОтладкиПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьСерверОтладки(ПараметрСерверОтладки) Экспорт
	
	// BSLLS:NestedFunctionInParameters-off
	Утверждения.ПроверитьРавенство(ТипЗнч(ПараметрСерверОтладки), Тип("Строка"),
		ЛокализованныеСтроки.УтверждениеСерверОтладкиТип);
	// BSLLS:NestedFunctionInParameters-on
	
	Если ЗначениеЗаполнено(ПараметрСерверОтладки) Тогда
		СерверОтладки = ПараметрСерверОтладки;
	Иначе
		СерверОтладки = СерверОтладкиПоУмолчанию();
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьНачальныйРазмерКучи(ПараметрНачальныйРазмерКучи) Экспорт
	
	// BSLLS:NestedFunctionInParameters-off
	Утверждения.ПроверитьРавенство(ТипЗнч(ПараметрНачальныйРазмерКучи), Тип("Строка"),
		ЛокализованныеСтроки.УтверждениеРазмерКучиТип);
	// BSLLS:NestedFunctionInParameters-on
	
	Если ЗначениеЗаполнено(ПараметрНачальныйРазмерКучи) Тогда
		НачальныйРазмерКучи = ПараметрНачальныйРазмерКучи;
	Иначе
		НачальныйРазмерКучи = Неопределено;
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьМаксимальныйРазмерКучи(ПараметрМаксимальныйРазмерКучи) Экспорт
	
	// BSLLS:NestedFunctionInParameters-off
	Утверждения.ПроверитьРавенство(ТипЗнч(ПараметрМаксимальныйРазмерКучи), Тип("Строка"),
		ЛокализованныеСтроки.УтверждениеРазмерКучиТип);
	// BSLLS:NestedFunctionInParameters-on
	
	Если ЗначениеЗаполнено(ПараметрМаксимальныйРазмерКучи) Тогда
		МаксимальныйРазмерКучи = ПараметрМаксимальныйРазмерКучи;
	Иначе
		МаксимальныйРазмерКучи = Неопределено;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьПараметрыКоманды(Команда) Экспорт
	
	Если ЗначениеЗаполнено(ПредметОтладки) Тогда
		Команда.ДобавитьПараметр(СтрШаблон("--infobase=""%1""", ПредметОтладки));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СерверОтладки) Тогда
		Команда.ДобавитьПараметр(СтрШаблон("--debugger=""%1""", СерверОтладки));
	КонецЕсли;

	Если ЗначениеЗаполнено(НачальныйРазмерКучи) Тогда
		Команда.ДобавитьПараметр(СтрШаблон("-Xms%1", НачальныйРазмерКучи));
	КонецЕсли;

	Если ЗначениеЗаполнено(МаксимальныйРазмерКучи) Тогда
		Команда.ДобавитьПараметр(СтрШаблон("-Xmx%1", МаксимальныйРазмерКучи));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриСозданииОбъекта() Экспорт
	
	ПредметОтладки = ПредметОтладкиПоУмолчанию();
	СерверОтладки = СерверОтладкиПоУмолчанию();
	
	НачальныйРазмерКучи = Неопределено;
	МаксимальныйРазмерКучи = Неопределено;
		
	ЛокализованныеСтроки = ЛокализованныеРесурсыПараметрыСоединенияЗамера.ЛокализованныеСтроки();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПредметОтладкиПоУмолчанию()
	Возврат "DefAlias";
КонецФункции

Функция СерверОтладкиПоУмолчанию()
	Возврат "http://localhost:1550";
КонецФункции

#КонецОбласти
